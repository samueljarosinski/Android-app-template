plugins {
    id 'com.android.application'

    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

apply from: rootProject.file('gradle/checks/checkstyle/checkstyle.gradle')
apply from: rootProject.file('gradle/checks/detekt/detekt.gradle')

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId 'com.example'

        versionName '0.1.0'
        versionCode 1

        minSdkVersion 23
        targetSdkVersion 30

        vectorDrawables.useSupportLibrary true
        resConfigs 'en'
    }

    signingConfigs {
        release {
            def signingFile = rootProject.file('signing.properties')

            if (signingFile.canRead()) {
                Properties signingProperties = new Properties()
                signingProperties.load(new FileInputStream(signingFile))

                storeFile rootProject.file(signingProperties['storeFile'])
                storePassword signingProperties['storePassword']
                keyAlias signingProperties['keyAlias']
                keyPassword signingProperties['keyPassword']
            }
        }

        debug {
            storeFile rootProject.file('debug.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        incremental true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        allWarningsAsErrors = true
        jvmTarget = '1.8'
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
        maxProcessCount 4
        javaMaxHeapSize '4g'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    kapt {
        useBuildCache true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError true
        warningsAsErrors true
        ignoreWarnings false
        checkDependencies true
        ignoreTestSources false
        xmlReport false
        lintConfig file("${project.rootDir}/gradle/checks/lint/lint.xml")
        htmlOutput file("${project.buildDir}/reports/lint/lint.html")
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources true
            returnDefaultValues true

            all { jvmArgs '-noverify' }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
}
